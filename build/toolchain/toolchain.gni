# Copyright 2015 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# Toolchain-related configuration that may be needed outside the context of the
# toolchain() rules themselves.

import("//build_overrides/build.gni")

declare_args() {
  # If this is set to true, we use the revision in the llvm repo to determine
  # the CLANG_REVISION to use, instead of the version hard-coded into
  # //tools/clang/scripts/update.py. This should only be used in
  # conjunction with setting the llvm_force_head_revision DEPS variable when
  # `gclient runhooks` is run as well.
  llvm_force_head_revision = false

  # Cronet is shipped in AOSP, where it is built using the Android Mainline
  # Clang. Please refer to go/cronet-builders-with-mainline-clang-design for
  # more information.
  # If this arg is set to true, we use the Android Mainline LLVM.
  llvm_android_mainline = false

  # Used for binary size analysis.
  generate_linker_map = is_android && is_official_build

  # Whether this toolchain is to be used for building host tools that are
  # consumed during the build process. That includes proc macros and Cargo build
  # scripts.
  toolchain_for_rust_host_build_tools = false

  # If false, the toolchain overrides `use_partition_alloc_as_malloc` in
  # PartitionAlloc, to allow use of the system allocator.
  toolchain_allows_use_partition_alloc_as_malloc = true
}

if (generate_linker_map) {
  assert(is_official_build || is_castos || is_cast_android,
         "Linker map files should only be generated when is_official_build = " +
             "true or is_castos = true or is_cast_android = true")
  assert(current_os == "android" || current_os == "linux" ||
             target_os == "android" || target_os == "linux" ||
             target_os == "chromeos",
         "Linker map files should only be generated for Android, Linux, " +
             "or ChromeOS.")
}

declare_args() {
  clang_version = exec_script("//tools/clang/scripts/get_clang_version.py",
                                [ "--full-version" ],
                                "trim string")

  clang_major_version = exec_script("//tools/clang/scripts/get_clang_version.py",
                                    [],
                                    "value")
}

# Extension for shared library files (including leading dot).
shlib_extension = ".so"

# Same extension but for the host platform. We have significantly fewer host
# platforms.
host_shlib_extension = ".so"

# Prefix for shared library files.
shlib_prefix = "lib"

# Directory for shared library files.
shlib_subdir = ""

# While other "tool"s in a toolchain are specific to the target of that
# toolchain, the "stamp" and "copy" tools are really generic to the host;
# but each toolchain must define them separately.  GN doesn't allow a
# template instantiation inside a toolchain definition, so some boilerplate
# has to be repeated in each toolchain to define these two tools.  These
# four variables reduce the duplication in that boilerplate.
stamp_description = "STAMP {{output}}"
copy_description = "COPY {{source}} {{output}}"
stamp_command = "touch {{output}}"
copy_command = "ln -f {{source}} {{output}} 2>/dev/null || (rm -rf {{output}} && cp -af {{source}} {{output}})"

# This variable is true if the current toolchain is one of the target
# toolchains, i.e. a toolchain which is being used to build the main Chrome
# binary. This generally means "not the host toolchain", but in the case where
# we're targeting the host it's true then as well. We do require current_os to
# match target_os so that for example we avoid considering Android as a target
# toolchain when targeting CrOS.
is_a_target_toolchain =
    (current_toolchain != host_toolchain ||
     default_toolchain == host_toolchain) && current_os == target_os

# A toolchain for building tools that run on the host machine and need to use
# the system allocator. This toolchain does not use PartitionAlloc-Everywhere by
# design. We use a name with `_host` injected into it to avoid colliding with
# toolchains of the same name (but different path) between different OSes.
host_system_allocator_toolchain = "${host_toolchain}_host_with_system_allocator"

# A toolchain for building tools that run on the default target machine and need
# to use the system allocator. This toolchain does not use
# PartitionAlloc-Everywhere by design.
default_system_allocator_toolchain =
    "${default_toolchain}_with_system_allocator"
